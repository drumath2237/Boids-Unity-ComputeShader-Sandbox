#pragma kernel CSMain

struct BoidsData
{
    float3 position;
    float3 velocity;
};

int boidsCount;

float effectRange;
float maxVelocity;
float maxAcceleration;
float3 boundary;
float deltaTime;

float fleeThreshold;
float alignWeight;
float separationWeight;
float cohesionWeight;
float wallForceWeight;
float wallDistanceWeight;

RWStructuredBuffer<BoidsData> boidsData;

const float3 ZeroVector3 = float3(0, 0, 0);

/// \brief Boid同士が範囲にあるかを判定する関数
/// \param self 基準となるBoid
/// \param other 判定対象のBoid
/// \param range 範囲
/// \return 範囲内かどうかのbool
bool WithinRange(const BoidsData self, const BoidsData other, const float range)
{
    const float3 dir = other.position - self.position;
    return dot(dir, dir) < range * range;
}

/// \brief ベクトル長を特定の長さに制限する関数
/// \param vec 3次元ベクトル
/// \param length 制限する長さ
/// \return 制限処理を行った後のベクトル。長さが0であればそのまま返す
float3 LimitVector(const float3 vec, const float length)
{
    return dot(vec, vec) == 0 ? vec : normalize(vec) * length;
}

/// \brief 整列処理によって周りのBoidと速度を合わせるような加速度を計算
/// \param index 計算対象のindex
/// \return Align処理で計算された加速度
float3 AlignForce(const int index)
{
    const BoidsData self = boidsData[index];

    float3 sumVelocity = ZeroVector3;
    int count = 0;

    for (int i = 0; i < boidsCount; i++)
    {
        const bool shouldCompute = i != index && WithinRange(self, boidsData[i], effectRange);
        if (!shouldCompute)
        {
            continue;
        }

        sumVelocity += boidsData[i].velocity;
        count++;
    }

    if (count == 0)
    {
        return ZeroVector3;
    }

    const float3 avgVelocity = sumVelocity / count;
    return avgVelocity - self.velocity;
}

/// \brief 分離処理。周りのboidが近すぎた場合に回避するような加速度を計算
/// \param index 計算対象のindex
/// \return 分離処理を行った際の計算された加速度
float3 SeparationForce(const int index)
{
    const BoidsData self = boidsData[index];

    float3 fleeForce = ZeroVector3;
    for (int i = 0; i < boidsCount; i++)
    {
        const BoidsData other = boidsData[i];
        const bool shouldCompute = i != index && WithinRange(self, other, effectRange);
        if (!shouldCompute)
        {
            continue;
        }

        const float3 dirPosition = other.position - self.position;
        if (dot(dirPosition, dirPosition) >= fleeThreshold * fleeThreshold)
        {
            continue;
        }
        fleeForce += -(dirPosition - self.velocity);
    }

    return fleeForce;
}

/// \brief 結合処理。周りのboidに近づくような加速度を計算
/// \param index 計算対象のboidsのindex
/// \return 加速度
float3 CohesionForce(const int index)
{
    const BoidsData self = boidsData[index];

    float3 sumPosition = ZeroVector3;
    int count = 0;

    for (int i = 0; i < boidsCount; i++)
    {
        const BoidsData other = boidsData[i];
        const bool shouldCompute = i != index && WithinRange(self, other, effectRange);
        if (!shouldCompute)
        {
            continue;
        }

        sumPosition += other.position;
        count++;
    }

    if (count == 0)
    {
        return ZeroVector3;
    }

    const float3 avgPosition = sumPosition / count;
    const float3 dirPosition = avgPosition - self.position;
    const float3 seekForce = dirPosition - self.velocity;

    return seekForce;
}

/// \brief 境界処理
/// \param boid boid
/// \param boundary 境界
/// \return 境界処理を行ったboid
BoidsData BorderTreatment(const BoidsData boid, const float3 boundary)
{
    float3 pos = boid.position;
    float3 vel = boid.velocity;

    if (pos.x > boundary.x)
    {
        pos.x = boundary.x;
        vel.x = -vel.x;
    }
    else if (pos.x < -boundary.x)
    {
        pos.x = -boundary.x;
        vel.x = -vel.x;
    }

    if (pos.y > boundary.y)
    {
        pos.y = boundary.y;
        vel.y = -vel.y;
    }
    else if (pos.y < -boundary.y)
    {
        pos.y = -boundary.y;
        vel.y = -vel.y;
    }

    if (pos.z > boundary.z)
    {
        pos.z = boundary.z;
        vel.z = -vel.z;
    }
    else if (pos.z < -boundary.z)
    {
        pos.z = -boundary.z;
        vel.z = -vel.z;
    }

    BoidsData result;
    result.position = pos;
    result.velocity = vel;

    return result;
}

float3 WallForce(const int index, const float distanceWeight)
{
    BoidsData boid = boidsData[index];
    return
        float3(-1, 0, 0) / abs((boundary.x - boid.position.x) / distanceWeight)
        + float3(1, 0, 0) / abs((-boundary.x - boid.position.x) / distanceWeight)
        + float3(0, -1, 0) / abs((boundary.y - boid.position.y) / distanceWeight)
        + float3(0, 1, 0) / abs((-boundary.y - boid.position.y) / distanceWeight)
        + float3(0, 0, -1) / abs((boundary.z - boid.position.z) / distanceWeight)
        + float3(0, 0, 1) / abs((-boundary.z - boid.position.z) / distanceWeight);
}

float3 CalcIndividualForce(const float index)
{
    const float3 align = AlignForce(index);
    const float3 separation = SeparationForce(index);
    const float3 cohesion = CohesionForce(index);

    return alignWeight * align
        + separationWeight * separation
        + cohesionWeight * cohesion;
}

[numthreads(64,1,1)]
void CSMain(const uint3 id:SV_DispatchThreadID)
{
    BoidsData boid = boidsData[id.x];

    float3 force = CalcIndividualForce(id.x);
    force = LimitVector(force, maxAcceleration);

    force += wallForceWeight * WallForce(id.x, wallDistanceWeight);

    boid.velocity += force * deltaTime;
    boid.velocity = LimitVector(boid.velocity, maxVelocity);

    boid.position += boid.velocity * deltaTime;
    // boid = BorderTreatment(boid, boundary);

    boidsData[id.x] = boid;
}
